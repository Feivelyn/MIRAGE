class CAiGoalSpecialSEASAttack inherit CAiGoalGeneralAttack

	var CAiObjWrap 		m_xLastEnemy;
	var CAiObjWrapList	m_xAttackers;
	var real			m_fAttackStrength;
	var string			m_sDifficulty;
	
	
	///Init
	export proc bool Init(^CAiBrain p_pxBrain)
		super.Init(p_pxBrain);
		if(p_pxBrain==null)then return false; endif;
		
		m_pxBrain = p_pxBrain;
		if(m_pxBrain==null)then return false; endif;
		
		m_pxSensor=^(m_pxBrain^.GetSensor());
		if(m_pxSensor==null) then return false; endif;
		
		m_iPlayerID=m_pxBrain^.GetPlayerId();
		m_sPlayerID=m_iPlayerID.ToString();
		m_sLevelName=CAiUtilityMgr.Get()^.GetLevelName(m_pxBrain);
		
		var int iNumPlayers=1+m_pxSensor^.GetNumPlayers();
//		var int iType = 1+Random.GetInt()%iNumPlayers;
		var int iType = 1;
		
		var ^CAiModuleControlDefault pxCM=cast<CAiModuleControlDefault>(m_pxBrain^.GetControlModule());
		SetThinkWait(pxCM^.GetDifficultyInt()+iType);
		
		m_sDifficulty=pxCM^.GetDifficulty();
		var int iDifficulty=pxCM^.GetOldDifficultyInt();
		
		m_sState = "new";
		
		m_xOnEnemiesFound = OnEnemiesFound;
		m_xOnCalculateEnemyValue = OnCalculateValue;
		
		GetEnemyQuery().AddMyEnemiesToSearch(m_iPlayerID, true);
		GetEnemyQuery().SetType("CHTR");
		GetEnemyQuery().SetType("ANML",true);
		GetEnemyQuery().SetType("VHCL",true);
		GetEnemyQuery().SetType("BLDG",true);
				
		if(iDifficulty==0)then
			m_fAttackStrength=0.15;
		elseif(iDifficulty==1)then
			m_fAttackStrength=0.2;
		elseif(iDifficulty==2)then
			m_fAttackStrength=0.25;
		elseif(iDifficulty==3)then
			m_fAttackStrength=0.3;
		elseif(iDifficulty==4)then
			m_fAttackStrength=0.35;
		elseif(iDifficulty==5)then
			m_fAttackStrength=0.4;
		elseif(iDifficulty==6)then
			m_fAttackStrength=0.45;
		elseif(iDifficulty==7)then
			m_fAttackStrength=0.5;
		elseif(iDifficulty==8)then
			m_fAttackStrength=0.55;
		else
			m_fAttackStrength=0.6;
		endif;
		
		if(m_sLevelName=="Single 09")then
			m_fAttackStrength=0.4;
		endif;
		
		if(m_sLevelName=="Single 10"&& iDifficulty>6)then
			m_fAttackStrength=0.4;
		endif;
			
		return true;
	endproc;


	///OnEnemiesFound
	proc void OnEnemiesFound(ref CAiObjWrapList p_rxList)
		m_xEnemies.CopyFrom(p_rxList);
		if(m_xEnemies.NumEntries()>0)then
			m_xLastEnemy = m_xEnemies[0];
		endif;
	endproc;
	
	
	///OnCalculateValue
	proc void OnCalculateValue(ref CAiObjWrap p_rxEnemy, ref real p_rfValue)
		if(p_rxEnemy.GetType()=="BLDG" || p_rxEnemy.GetClassName()=="aje_resource_collector")then
			p_rfValue-=10.0;
		elseif(p_rxEnemy.IsSameAs(m_xLastEnemy))then
			p_rfValue+=0.1;
		endif;
	endproc;


	///Think
	export proc void Think()
		super.Think();
		if(m_pxBrain==null)then return; endif;
		
		var ^CAiGoalMinistryOfDefense	pxMOD = GetMOD();		
		if(pxMOD == null)then return; endif;
		
		var ^CAiModuleControlDefault pxCM=cast<CAiModuleControlDefault>(m_pxBrain^.GetControlModule());
		var string sDifficulty=pxCM^.GetDifficulty();
		
		var int iMinUnits, iMaxUnits;
		var real fBad;
		if(m_sState=="new")then
			var int iAge = Math.Min(CAiUtilityMgr.Get()^.GetCurrentAge(m_pxBrain), 1);
			if(!GetUnits("SpecialSEASAttackAttack_"+iAge.ToString()))then
				var CAiObjWrapList xUnits;
				m_xAllocator.StartAsSupervisor();
				if(sDifficulty=="Easy")then
					iMinUnits=1;  iMaxUnits=4; fBad=1.0;
				elseif(sDifficulty=="Medium")then
					iMinUnits=1;  iMaxUnits=6; fBad=0.75;
				else
					iMinUnits=1;  iMaxUnits=8; fBad=0.5;
				endif;
				if(pxMOD^.QueryUnits(xUnits, iMinUnits, iMaxUnits, fBad, 0, m_xEnemies) == false)then
					m_pxAttackInfo^.GetResult()="squad allocation failed (impossible)";
					m_pxAttackInfo^.NotifyOnFailed();
					Deactivate();
				else
	
					var int i, iC=xUnits.NumEntries();
					var int iTops=0;
					if(iC>0)then
						for(i=0)cond(i<iC)iter(i++)do
							if(xUnits[i].IsValid())then
								if(xUnits[i].GetClassName()=="seas_triceratops")then
									iTops++;
								endif;
							endif;						
						endfor;
					endif;
					if(iTops==0)then
						var CAiObjWrap xObj=pxCM^.GetCheatMgr().SpawnUnit("seas_triceratops");
						if(xObj.IsValid())then
							xUnits.AddEntry(xObj);
						endif;
					endif;
		
					m_xAllocator.AddUnits(xUnits);
					m_xAllocator.AddUnits(m_xEssentialUnitsFromMOD);
					m_xAttackers.Clear();
					m_xAttackers.Include(xUnits);
					m_xAttackers.Include(m_xEssentialUnitsFromMOD);
					m_xEssentialUnitsFromMOD.Clear();					
					m_xEnemies.CopyFrom(m_pxAttackInfo^.GetTargets());
					pxMOD^.RemoveUnitsFromPool(m_xAllocator.GetUnits());
					SeparateUnits();
					
					if(TransferUnits() == true)then
						m_sState = "transfering";
					else
						StartSitOn();
						m_sState = "sit_on";
					endif;
				endif;
				return;
			endif;
			m_sState="waiting";
		elseif(m_sState=="waiting")then
			if(m_xAllocator.IsRunning())then
				var CAiObjWrapList xUnits;				
				if(sDifficulty=="Easy")then
					iMinUnits=1;  iMaxUnits=4; fBad=1.0;
				elseif(sDifficulty=="Medium")then
					iMinUnits=1;  iMaxUnits=6; fBad=1.0;
				else
					iMinUnits=1;  iMaxUnits=8; fBad=1.0;
				endif;
				if(pxMOD^.QueryUnits(xUnits, iMinUnits, iMaxUnits, fBad, 0, m_xEnemies) == false)then
					m_pxAttackInfo^.GetResult()="squad allocation failed (impossible)";
					m_pxAttackInfo^.NotifyOnFailed();
					Deactivate();
				else
					var int i, iC=xUnits.NumEntries();
					var int iTops=0;
					if(iC>0)then	
						for(i=0)cond(i<iC)iter(i++)do
							if(xUnits[i].IsValid())then
								if(xUnits[i].GetClassName()=="seas_triceratops")then
									iTops++;
								endif;
							endif;						
						endfor;
					endif;
					if(iTops==0)then
						var CAiObjWrap xObj=pxCM^.GetCheatMgr().SpawnUnit("seas_triceratops");
						if(xObj.IsValid())then
							xUnits.AddEntry(xObj);
						endif;
					endif;
					
					m_xAllocator.AddUnits(xUnits);
					m_xAllocator.AddUnits(m_xEssentialUnitsFromMOD);
					m_xAttackers.Clear();
					m_xAttackers.Include(xUnits);
					m_xAttackers.Include(m_xEssentialUnitsFromMOD);
					m_xEssentialUnitsFromMOD.Clear();
					m_xEnemies.CopyFrom(m_pxAttackInfo^.GetTargets());
					var bool bEnough;
					if(!CompareLevel(m_xEnemies, m_xAttackers, m_pxBrain,bEnough))then
						if(!bEnough)then
							m_pxAttackInfo^.GetResult()="Enemy is to strong!";
							m_pxAttackInfo^.NotifyOnFailed();
							Deactivate();
							return;
						endif;
					endif;
					
					pxMOD^.RemoveUnitsFromPool(m_xAllocator.GetUnits());
					SeparateUnits();
					if(TransferUnits() == true)then
						m_sState = "transfering";
					else
						StartSitOn();
						m_sState = "sit_on";
					endif;
				endif;
			elseif(m_xAllocator.IsNotPossible())then
				m_pxAttackInfo^.GetResult()="squad allcation failed!";
				m_pxAttackInfo^.NotifyOnFailed();
				Deactivate();
				return;
			elseif(m_xAllocator.IsAborted())then
				m_pxAttackInfo^.GetResult()="squad dead";
				m_pxAttackInfo^.NotifyOnFailed();
				Deactivate();
				return;
			endif;
		elseif(m_sState=="transfering")then
			if(m_bTransferSucceeded == true)then
				StartSitOn();
				m_sState = "sit_on";
			endif;
		elseif(m_sState=="sit_on")then
			if(m_bTransportersReady == true)then
				StartAllAttacks();
				m_sState = "fighting";
			endif;
		elseif(m_sState=="fighting")then
			if(!UpdateFighters())then
				m_pxAttackInfo^.GetResult()="all fighters are dead!";
				SetFailedPosition(m_vFightersPos);
				m_pxAttackInfo^.NotifyOnFailed();
				Deactivate();
				return;
			endif;
			
			if(CheckCompletion())then
				m_pxAttackInfo^.GetResult()="attack succeded!";
				m_pxAttackInfo^.NotifyOnSucceeded();
				Deactivate();
				return;
			endif;
			
			if(UpdateNearbyEnemies())then
				UpdateAllAttacks();
			endif;
		endif;
			
	endproc;
	
	
	///CompareLevel
	export proc bool CompareLevel(CAiObjWrapList p_xTargets, CAiObjWrapList p_xActors, ^CAiBrain p_pxBrain, ref bool p_rbEnough)
		if(p_pxBrain==null)then return true; endif;
		if(p_xActors.NumEntries()<=0)then return false; endif;
		if(p_xTargets.NumEntries()<=0)then return true; endif;
		
		var int 	iNumActors=p_xActors.NumEntries();
		var int 	i, iC=p_xTargets.NumEntries();
		var int 	j, iD=p_xActors.NumEntries();
		var int		iZero=0; var int iOne=0; var int iTwo=0; var int iThree=0; var int iFour=0;
		var int		iNull=0; var int iEins=0; var int iZwei=0; var int iDrei=0; var int iVier=0;
		var int		iTotalTargets=0;
		var int		iTotalActors=0;
		var bool	bLevelOK=true;
		var real	fSixty=0.0;
		
		var ^CAiGoalMinistryOfDefense pxMod=CAiUtilityMgr.Get()^.GetMOD(p_pxBrain);
		if(pxMod==null)then return true; endif;
				
		for(i=0)cond(i<iC)iter(i++)do
			if(!p_xTargets[i].IsValid())then continue; endif;
			
			if(p_xTargets[i].GetClassName().Find("_worker")!=(-1))then continue; endif;
			
			if((p_xTargets[i].GetType()=="CHTR")||(p_xTargets[i].GetType()=="ANML")||(p_xTargets[i].GetType()=="VHCL")||(p_xTargets[i].GetType()=="SHIP"))then
				if(p_xTargets[i].GetAttribValueInt("level")==0)then
					iNull++;
				elseif(p_xTargets[i].GetAttribValueInt("level")==1)then
					iEins+=2;
				elseif(p_xTargets[i].GetAttribValueInt("level")==2)then
					iZwei+=3;
				elseif(p_xTargets[i].GetAttribValueInt("level")==3)then
					iDrei+=4;
				elseif(p_xTargets[i].GetAttribValueInt("level")==4)then
					iVier+=5;
				endif;
			endif;
		endfor;		
		iTotalTargets=(iNull+iEins+iZwei+iDrei+iVier)*2;
		
		for(j=0)cond(j<iD)iter(j++)do		
			if(!p_xActors[j].IsValid())then continue; endif;				
			
			if(p_xActors[j].GetAttribValueInt("level")==0)then
				iZero++;
			elseif(p_xActors[j].GetAttribValueInt("level")==1)then
				iOne+=2;
			elseif(p_xActors[j].GetAttribValueInt("level")==2)then
				iTwo+=3;
			elseif(p_xActors[j].GetAttribValueInt("level")==3)then
				iThree+=4;
			elseif(p_xActors[j].GetAttribValueInt("level")==4)then
				iFour+=5;
			endif;
		endfor;		
		iTotalActors=(iZero+iOne+iTwo+iThree+iFour)*2;
		
		fSixty=iTotalTargets.ToReal()*m_fAttackStrength;
		p_rbEnough=(iThree>=8||(iThree>=4&&iFour>=5))&&iTwo>=15&&(iOne>=24||(iOne>=18&&iZero>=4));
		
		if(fSixty>iTotalActors.ToReal())then
			bLevelOK=false;
		else
			bLevelOK=true;
		endif;
		
		return bLevelOK;
	endproc;

endclass;
