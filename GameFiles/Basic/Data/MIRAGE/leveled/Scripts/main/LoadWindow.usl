class CLoadWindow inherit CFrameWindow;

	export var bool				m_bLoadLevelWindow;	
	export var ^CProgressBar	m_pxBar;
	var	^CPane					m_pxBG;
	var	^CStaticCtrl			m_pxTmp;
	
	export constructor(string p_sTopic)

		var ^CDesktop pxDesktop=CClientWrap.GetDesktop();
		if(!InitFromResource("main/LoadWindow","LoadWindow")) then
			CLevelEditorWrap.LogError("UI.LoadWindow","InitFromResource failed");
			return;
		endif;
		
		m_pxBG=new CPane();
		m_pxBG^.SetPos(0,0);
		var color xColor={{0,0,0,255}};
		m_pxBG^.SetColor(xColor);
		m_pxBG^.SetSize(pxDesktop^.GetSize().GetX(),pxDesktop^.GetSize().GetY());
		m_pxBG^.SetVisible(true);
		pxDesktop^.AddChild(m_pxBG);
		
		m_pxTmp=new CStaticCtrl();
		m_pxTmp^.SetBitmap("menue/decoration/loading_temp.tga");	
		m_pxTmp^.SetPos(pxDesktop^.GetSize().GetX()/2 - 192,pxDesktop^.GetSize().GetY()/2 - 214);
		m_pxTmp^.SetTransparent(true);
		m_pxTmp^.SetBackground(true);
		pxDesktop^.AddChild(m_pxTmp);
		
		SetPos(pxDesktop^.GetSize().GetX()/2 - GetSize().GetX()/2,pxDesktop^.GetSize().GetY()/2 - (GetSize().GetY()/2) + 180);
		var ^CStaticCtrl pxStatic1 = cast<CStaticCtrl>(GetControl("Stc_Info1"));
		pxStatic1^.SetText(CLocalizer.Get().Translate("_LoadWindow_Please_Wait"));
		var ^CStaticCtrl pxStatic2 = cast<CStaticCtrl>(GetControl("Stc_Info2"));
		pxStatic2^.SetText(p_sTopic);
		m_pxBar = cast<CProgressBar>(GetControl("Prg_Bar"));
		m_pxBar^.SetRange(1.0);
		//m_pxBar^.SetBackgroundColor({{255,0,0,255}});
		m_pxBar^.SetBarColor({{255,50,0,255}});
	endconstructor;
	
	export destructor()
		m_pxBG^.Destroy();
		m_pxTmp^.Destroy();
	enddestructor;
	
	export proc void SetText(string p_sTopic);
		var ^CStaticCtrl pxStatic2 = cast<CStaticCtrl>(GetControl("Stc_Info2"));
		pxStatic2^.SetText(p_sTopic);
	endproc;
	
	export proc void SetProgress(real p_fValue, string p_sDescription)
		SetProgress(p_fValue, p_sDescription, "Preparing ");
	endproc;
	
	export proc void SetProgress(real p_fValue, string p_sDescription, string p_sPrefix)
		m_pxBar^.SetProgress(p_fValue);
		if(p_sDescription!="" && p_sDescription!="Loading")then
			var ^CStaticCtrl pxStatic2 = cast<CStaticCtrl>(GetControl("Stc_Info2"));
			pxStatic2^.SetText(p_sPrefix+p_sDescription+"...");
		endif;
	endproc;
endclass;
